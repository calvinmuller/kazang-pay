plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

android {
    namespace = "net.kazang.pegasus"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig true
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId = "net.kazang.pegasus"
        minSdk = 26
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        release {
            if (System.getenv()["CI"]) { // CI=true is exported by Codemagic
                storeFile file(System.getenv()["CM_KEYSTORE_PATH"])
                storePassword System.getenv()["CM_KEYSTORE_PASSWORD"]
                keyAlias System.getenv()["CM_KEY_ALIAS"]
                keyPassword System.getenv()["CM_KEY_PASSWORD"]
            } else {
                storeFile file("Untitled")
                storePassword "kp2025"
                keyAlias "kp"
                keyPassword "kp2025"
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        // Allow all architectures in debug builds
        if (gradle.startParameter.taskNames.any { it.toLowerCase().contains('release') }) {
            exclude 'lib/arm64-v8a/**'
            exclude 'lib/x86/**'
            exclude 'lib/x86_64/**'
        }
    }

    flavorDimensions "brand", "env"

    productFlavors {
        sunmi {
            dimension "brand"
        }
        urovo {
            dimension "brand"
        }

        uat {
            dimension "env"
            resValue "string", "app_name", "Kazang Pay UAT"
            applicationIdSuffix = ".dev"
            versionNameSuffix "-uat"
        }
        prod {
            dimension "env"
            resValue "string", "app_name", "Kazang Pay"
            applicationIdSuffix = ""
        }
    }
}

flutter {
    source = "../.."
    target = "lib/main.dart"
}

dependencies {
    implementation files('libs/prismcore-release-1.0.15.aar')
    implementation files('libs/prismiso8583-release-1.0.10.aar')

    urovoImplementation files('libs/prismdevice-urovo-release-1.0.21.aar')
    urovoImplementation files('libs/prismfactory-urovo-release-1.0.26.aar')

    sunmiImplementation files('libs/prismdevice-sunmi-release-1.0.21.aar')
    sunmiImplementation files('libs/prismfactory-sunmi-release-1.0.26.aar')
    sunmiImplementation("com.sunmi:printerx:1.0.17")

    implementation("com.squareup.retrofit2:retrofit:2.6.2")
    implementation("com.squareup.retrofit2:converter-gson:2.6.2")
    implementation("com.squareup.retrofit2:adapter-rxjava2:2.6.2")
    implementation("com.squareup.retrofit2:converter-scalars:2.6.2")
    implementation("com.squareup.okhttp3:logging-interceptor:3.9.0")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.1")
    implementation "androidx.datastore:datastore-preferences:1.1.0"
    implementation "androidx.datastore:datastore-preferences-core:1.1.0"
    implementation "com.google.code.gson:gson:2.3.1"
    implementation("androidx.work:work-runtime:2.7.1")
}
